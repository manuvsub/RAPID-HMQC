prosol relations=<triple>
#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

define delay dNH

"d11=30m"
"d12=20u"
"dNH=1s/(cnst4*2)"
"in0=inf1"
 
"d0=0" 

"p41=31.977*(p62*1.0)"
"spw25=plw1*pow(p1/(p62*1.0), 2)"  
"spoff25=-0.013*100e-6/(p62*1.0)"   
;spnam25 z2iyAz2z120xA0.9cs_rf0.00_31.977p1_bw0.00p1.mrf

"p42=28.4*p62"
"spw26=plw1*pow(p1/p62, 2)" 
"spoff26=(2666.666)*100e-6/p62"  
;spnam26 BandSelRe4_rf0.00_28.400p1_bw0.00p1.mrf

"p8 =p3*10.021"    /* Universal PI/2 pulse  */
"spoff13=0.0"     /* Universal PI/2 pulse offset */
"spw13=plw2"      /* Universal PI/2 pulse power level  */
;spnam13 z2iz_rf0.00_10.021p1_bw2.40B1.mrf;


"DELTA1=dNH-p16-d16-p41*0.9"
"DELTA2=p41*0.9-de-4u"

1 ze 
  d11 pl16:f3
2 d1 do:f3
3 d12 pl3:f3
  50u UNBLKGRAD

  "d21 = d0 - p21*0.637*2"
  if "d21 < 0"
   { 
   "d21 = 0"
   }

  p16:gp2
  d16

  (p41:sp25 ph1):f1
  p16:gp1
  d16

#   ifdef LABEL_CN
  (center (p42:sp26 ph2):f1 (DELTA1 p21 ph3 d21 p21 ph4 DELTA1):f3  (p8:sp13 ph1):f2)
#   else
  (center (p42:sp26 ph2):f1 (DELTA1 p21 ph3 d21 p21 ph4 DELTA1):f3 )
#   endif /*LABEL_CN*/


  DELTA2
  p16:gp1
  d16 pl16:f3
  4u BLKGRAD
  go=2 ph31 cpd3:f3 
  d1 do:f3 mc #0 to 2 
     F1PH(calph(ph3, +90), caldel(d0, +in0))
exit 
  

ph1=0 
ph2=0 
ph3=0 2
ph4=0 0 2 2 
ph31=0 2 2 0


;pl3 : f3 channel - power level for pulse (default)
;pl26: f3 channel - power level for CPD/BB decoupling (low power)
;sp13: f2 channel - shaped pulse 180 degree (adiabatic)



;sp23: f1 channel - shaped pulse 120 degree 
;                   (Pc9_4_120.1000)
;sp24: f1 channel - shaped pulse 180 degree (Rsnob.1000)
;p8 : f2 channel - 180 degree shaped pulse for inversion (adiabatic)
;p16: homospoil/gradient pulse                       [1 msec]
;p21: 15N -  90 degree high power pulse
;d0 : incremented delay (2D) = in0/2-p21*4/3.1415
;d1 : relaxation delay
;d11: delay for disk I/O                             [30 msec]
;d12: delay for power switching                      [20 usec]
;d16: delay for homospoil/gradient recovery
;d21: internal delay
;dNH : 1/(2J)NH
;cnst4: = J(NH) 

;inf1: 1/SW(N) = 2 * DW(N)
;in0: 1/ SW(N) = 2 * DW(N)
;nd0: 1
;ns: 2 * n
;ds: 16
;aq: <= 50 msec
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEC
;cpd3: decoupling according to sequence defined by cpdprg3: garp4.p62
;pcpd3: f3 channel - 90 degree pulse for decoupling sequence
;          use pulse of >= 350 usec


;use gradient ratio:	gp 1 : gp 2
;			  11 :    7


;for z-only gradients:
;gpz1: 11%
;gpz2:  7%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100

 